import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
}

group = 'me.cheezburga'
version = '1.1.4'

repositories {
    mavenCentral()
    mavenLocal()
    maven {url = "https://repo.papermc.io/repository/maven-public/"}
    maven {url = "https://repo.skriptlang.org/releases"}
    maven {url = "https://jitpack.io"}
    maven {url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"}
    maven {url = "https://maven.enginehub.org/repo/"}
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT"
    compileOnly("com.github.SkriptLang:Skript:2.12.0") { transitive = false }

    implementation("com.sk89q.worldedit:worldedit-core:7.2.20")
    implementation("com.sk89q.worldedit:worldedit-bukkit:7.2.20")

    implementation("com.github.javaparser:javaparser-core:3.25.4")

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0-M1'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

def oldestJava = 17
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-source', '' + oldestJava, '-target', '' + oldestJava]
}

processResources {
    filesMatching(["plugin.yml", "lang/default.lang"]) {
        filter ReplaceTokens, tokens: ["version": project.version]
    }
}

tasks.register('addAnnotations', JavaExec) {
    mainClass = 'me.cheezburga.skwe.api.utils.EmptyAnnotationAdder'
    classpath = sourceSets.main.runtimeClasspath
    args = ["src/main/java"]
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
}
